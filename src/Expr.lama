-- Expression evaluator

import List;
import Array;
import State;
import World;
import Lexer;
import Parser;
import Fun;
import Builtins;

-- As association map which maps "\otimes" into "\oplus"
var ops = {
  ["+" , infix + ],
  ["-" , infix - ],
  ["*" , infix * ],
  ["/" , infix / ],
  ["%" , infix % ],
  ["==", infix ==],
  ["!=", infix !=],
  ["<" , infix < ],
  ["<=", infix <=],
  [">" , infix > ],
  [">=", infix >=],
  ["&&", infix &&],
  ["!!", infix !!]
};

-- The evaluator for infix operators: takes an operator as a string
-- and two operand values, and returns the result
public fun evalOp (op, l, r) {
  case assoc (ops, op) of
    Some (f) -> f (l, r)
  esac
}

-- Evaluates an expression "expr" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
--
-- A expression is represented by a data structure of the following shape:
--
-- expr = Assn    (expr, expr)         |
--        Set     (string, expr)       |
--        Seq     (expr, expr)         |
--        Skip                         |
--        if      (expr, expr, expr)   |
--        While   (expr, expr)         |
--        DoWhile (expr, expr)         |
--        Var     (string)             |
--        Ref     (string)             |
--        Const   (int)                |
--        Binop   (string, expr, expr) |
--        Call    (string, expr list)  |
--        Ignore  (expr)               |
--        String  (string)             |
--        Array   (expr list)          |
--        Elem    (expr, expr)         |
--        ElemRef (expr, expr)         |
--        Builtin (string, expr list)

-- Helper function: checks that given name designates a regular variable in
-- a given state
fun lookupVal (state, name) {
  case state.lookup (name) of
    x@Val (_) -> x
  | _         -> error (sprintf ("the name ""%s"" does not designate a variable", name), getLoc (name))
  esac
}

-- Helper function: checks that given name designates a function in
-- a given state
fun lookupFun (state, name) {
  case state.lookup (name) of
    x@Fun (_, _) -> x
  | _            -> error (sprintf ("the name ""%s"" does not designate a function", name), getLoc (name))
  esac
}

-- Helper function: adds a bunch of regular variables current scope
fun addNames (state, names) {
  foldl (fun (s, name) {s.addName (name, Val (0))}, state, names)
}

-- Helper function: adds a function in current scope
fun addFunction (state, name, args, body) {
  state.addName (name, Fun (args, body))
}
                                        
-- Evaluates a list of expressions, properly threading a configurations.
-- Returns the final configuration and the list of values
fun evalList (c, exprs) {
  case foldl (fun ([c, vals], e) {
                case eval (c, e) of
                  [c, v] -> [c, v : vals]
                esac
              },
              [c, {}],
              exprs) of
    [c, vals] -> [c, reverse (vals)]
  esac
}

fun unwrapVals(vals) {
  fun unwrapVal(v) {
    case v of
      Val(x) -> x
    esac
  }

  map(unwrapVal, vals)
}

fun eval (c@[st, w], expr) {
  case expr of
    Var     (name)                      -> [c, Val(st.lookup(name))]
  | Ref     (name)                      -> [c, Ref(name)]
  | Const   (n)                         -> [c, Val(n)]
  | Skip                                -> [c, Bot]
  | Ignore  (expr)                      -> 
    case evalList (c, {expr}) of
      [c, _] -> [c, Bot]
    esac
  | Seq     (exp1, exp2)                -> 
    case evalList (c, {exp1, exp2}) of
      [c, {_, ev_2}] -> [c, ev_2]
    esac
  | Binop   (op, l, r)                  -> 
    case evalList (c, {l, r}) of
      [c, {Val(ev_l), Val(ev_r)}] -> [c, Val(evalOp (op, ev_l, ev_r))]
    esac
  | Assn    (ref, rhs)                  ->
    case evalList (c, {ref, rhs}) of
      [[st1, w1], {Ref (name), Val(ev_rhs)}] -> [[st1 <- [name, ev_rhs], w1], Val(ev_rhs)]
    | [c1, {ElemRef (arr, idx), Val(ev_rhs)}] -> (
        arr[idx] := ev_rhs;
        [c1, Val(ev_rhs)]
      )
    esac
  | Set     (name, val_exp)  -> eval(c, Assn(Ref(name), val_exp))
  | If      (cond, then_exp, else_exp)  ->
    case eval (c, cond) of
      [c1, Val(v)] -> if v != 0 then
        eval (c1, then_exp)
      else
        eval (c1, else_exp)
      fi
    esac
  | While   (cond, body)                -> (
      var ev = [c, 0];
      while (
        case eval (ev.fst, cond) of
          [c, Val(v)] -> (
            ev := [c, v];
            v != 0
          )
        esac
      ) do
        ev := eval (ev.fst, body)
      od;
      [ev.fst, Bot]
    )
  | DoWhile (body, cond)                -> (
      var ev = [c, 0];
      do
        ev := eval (ev.fst, body)
      while (
        case eval (ev.fst, cond) of
          [c, Val(v)] -> (
            ev := [c, v];
            v != 0
          )
        esac
      ) od;
      [ev.fst, Bot]
    )
  | Scope (defs, expr)                  -> (
      fun addDef(st, def) {
        case def of
          Fun (name, args, body)  -> st.addFunction(name, args, body)
        | Var (names)             -> st.addNames(names)
        esac
      }
      var st1 = foldl(addDef, st.enterScope, defs);
      case eval ([st1, w], expr) of
        [[st2, w2], v] -> [[st2.leaveScope, w2], v]
      esac
    )

  | Call (name, args)                     -> (
      case evalList(c, args) of
        [[st, w], valueParams] -> 
          case st.lookup (name) of
            Fun(argNames, body) -> (
              fun addArg(st, [argName, valueParam]) {
                st.addName(argName, valueParam)
              }

              var unwrappedParams = unwrapVals(valueParams);
              case body of
                External  -> 
                  case evalBuiltin(name, unwrappedParams, w) of
                    [retVal, w1] -> [[st, w1], Val(retVal)]
                  esac
              | _         -> (
                  var st1 = foldl(addArg, st.enterFunction, zip(argNames, unwrappedParams));
                  case eval ([st1, w], body) of
                    [[st2, w2], retVal] -> [[st.leaveFunction(st2.getGlobal), w2], retVal]
                  esac
                )
              esac
            )
          esac
      esac
    )
  | String  (s)         -> [c, Val(s)]
  | Array   (elems)       -> 
    case evalList (c, elems) of
      [c, ev_elems] -> [c, Val(listArray(unwrapVals(ev_elems)))]
    esac
  | Elem    (arr_exp, loc_exp)  -> 
    case evalList(c, {arr_exp, loc_exp}) of
      [c, {Val(arr_val), Val(loc_val)}] -> (
        var items = case arr_val of
          Sexp(_, v)  -> v
        | v           -> v 
        esac;
        [c, Val(items[loc_val])]
      )
    esac
  | ElemRef (arr_exp, loc_exp)  -> 
    case evalList(c, {arr_exp, loc_exp}) of
      [c, {Val(arr_val), Val(loc_val)}] -> [c, ElemRef(arr_val, loc_val)]
    esac
  | Sexp    (name, vals)  -> 
    case evalList (c, vals) of
      [c, ev_elems] -> [c, Val(Sexp(name, listArray(unwrapVals(ev_elems))))]
    esac
  | _ -> failure ("unknown expression: %s\n", expr.string)
  esac
}


-- Evaluates a program with a given input and returns an output
public fun evalExpr (input, expr) {
  case eval ([emptyState ().enterScope.addName ("read",   Fun ({}, External))
                                      .addName ("write",  Fun ({"a"}, External))
                                      .addName ("length", Fun ({"a"}, External)), createWorld (input)], expr) of
    [c, _] -> c.snd.getOutput
  esac
}